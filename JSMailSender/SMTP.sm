%{
//
//SMTP FSM
//
%}

%start JSSMTPMap::Connecting
%class JSSMTPConnection
%header JSSMTPConnection.h

%map JSSMTPMap
%%

Connecting
    Entry {
        connect();
    }
    {
    success
        WaitingEHLOReply { }
    failure
        Disconnected {}
    error
        Disconnected {}
    }

WaitingEHLOReply
    Entry {
        sendEHLO();
    }
{
    success [ctxt.tls]
        //if -[JSSMTPConnection tls] == YES
        WaitingSTARTTLSReply { }
    success [ctxt.auth]
        //if -[JSSMTPConnection auth] == YES
        push(JSSMTPAuthMap::StartingAuth) {  }
    success
        //EHLO responsded, but without either TLS or AUTH options
        ReadyToQuit {}
    authenticated
        WaitingMAILReply {}
}

WaitingMAILReply
    Entry {
        sendMAIL();
    }
    {
    success
        WaitingRCPTReply{ }
}

WaitingRCPTReply
    Entry {
        sendRCPT();
    }
{
    success
        WaitingDATAReply { }
}

WaitingSTARTTLSReply
    Entry { sendSTARTTLS(); }
    {
        success TLSStarting {}
    }

TLSStarting 
    Entry { startTLS(); }
{
        success
            WaitingEHLOReply {  }
}

WaitingDATAReply 
    Entry { sendDATA(); }
    {
        success SendingData { }
    }

SendingData 
    Entry { sendMessage();  }
    {
    }   

ReadyToQuit 
    Entry { sendQUIT(); }
{
    Default
        Disconnected {  }
}

Disconnected 
    Entry {
        disconnect();
    }
{
    Default nil {}
}

Default 
{
    Default
        ReadyToQuit {  }
}


%%

%map JSSMTPAuthMap
%%
StartingAuth
    Entry { sendAUTH(); }
{
//    success [[ctxt.authMethod isEqualToString:@"CRAM-MD5"]]
//        WaitingCRAMMD5Reply { startCRAMMD5(); }
    success [[ctxt.authMethod isEqualToString:@"LOGIN"]]
        WaitingLOGINReply { startLOGIN(); }
    success [[ctxt.authMethod isEqualToString:@"PLAIN"]]
        WaitingPLAINReply { startPLAIN(); }
}

WaitingLOGINReply {
    success WaitingAuthenticationResult { sendLOGINPassword(); }
}

WaitingCRAMMD5Reply {
}

WaitingPLAINReply {

    success pop(authenticated) {}
    failure pop(failure) {}
}

WaitingAuthenticationResult {
    success pop(authenticated) {}
    failure pop(failure) {}
}
%%